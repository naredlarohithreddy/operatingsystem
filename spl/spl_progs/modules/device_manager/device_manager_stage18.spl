alias functionNum R1;
alias currentPID R2;
if(functionNum==3) then //terminal_write
    //calling acquire terminal
    multipush(R1,R2,R3);
    R1=8;
    R2=currentPID;
    call RESOURCE_MANAGER; //module 0
    multipop(R1,R2,R3);
    alias word R3;
    print word;
    //calling release terminal
    multipush(R1,R2,R3);
    R1=9;
    R2=currentPID;
    call RESOURCE_MANAGER; //module 0
    multipop(R1,R2,R3);
    return;
endif;
if(functionNum==4) then //terminal_read
    //calling acquire terminal
    multipush(R1,R2,R3);
    R1=8;
    R2=currentPID;
    call RESOURCE_MANAGER; //module 0
    multipop(R1,R2,R3);
    read;
    [PROCESS_TABLE+(currentPID*16)+4] = WAIT_TERMINAL;
    multipush(R1,R2,R3);
    call SCHEDULER;
    multipop(R1,R2,R3);
    alias wordAddress R3;
    alias physicalPageNum R4;
    alias offset R5;
    alias physicalAddress R6;
    physicalPageNum=[PTBR+2*(wordAddress/512)];
    offset=wordAddress%512;
    physicalAddress=physicalPageNum*512+offset;
    [physicalAddress]=[PROCESS_TABLE+(currentPID*16)+8]; //storing input buffer of process
    return;
endif;
if(functionNum==2) then // Disk load
    alias pageNum R3;
    alias blockNum R4;
    multipush(R0,R1,R2,R3,R4);
    R1=3; // acquire Disk
    R2=currentPID;
    call RESOURCE_MANAGER;
    multipop(R0,R1,R2,R3,R4);
    [DISK_STATUS_TABLE+1]=0;
    [DISK_STATUS_TABLE+2]=pageNum;
    [DISK_STATUS_TABLE+3]=blockNum;
    load(pageNum,blockNum);
    [PROCESS_TABLE+(currentPID*16)+4] = WAIT_DISK;
    multipush(R0,R1,R2,R3,R4);
    call SCHEDULER;
    multipop(R0,R1,R2,R3,R4);
    return;
endif;